/*
   moise-exceptions Gradle file

*/

import groovy.swing.SwingBuilder

plugins {
  id 'java'
  id 'eclipse'
  id 'maven-publish'
}

defaultTasks 'jar'

version '0.3'
group   'it.unito.di'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()

    maven { url "https://raw.githubusercontent.com/jacamo-lang/mvn-repo/master" }

}

configurations {
	  sshAntTask
    //umljavadoc  // see http://www.umlgraph.org/doc/indexw.html
    extraLibs
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    implementation group: 'org.jason-lang', name: 'jason',    version: '2.5.2', changing: false
    implementation group: 'org.jacamo',     name: 'npl' ,     version: '0.5', changing: true, transitive: false
    implementation group: 'org.jacamo',     name: 'cartago',  version: '3.0', changing: true
    implementation group: 'org.jacamo',     name: 'jaca',     version: '3.0', changing: true

    implementation group: 'junit',      name: 'junit', version: '4.+'

    implementation group: 'guru.nidi', name: 'graphviz-java', version: '0.7.0'

    //umljavadoc 'org.umlgraph:umlgraph:5.6'

    implementation 'com.google.code.gson:gson:2.8.6'

	sshAntTask 'org.apache.ant:ant-jsch:1.7.1', 'jsch:jsch:0.1.29'

    extraLibs group: 'org.jacamo',     name: 'npl' ,     version: '0.5-SNAPSHOT', changing: true, transitive: false
    extraLibs 'com.google.code.gson:gson:2.8.6'
}

jar {
    baseName 'moise-exceptions'
    into('') {
        from configurations.extraLibs
    }
}

clean {
    delete 'bin'
    delete 'doc/api'
    delete 'test.xml'
}

task intsim (type: JavaExec, dependsOn: 'jar') {
    description 'runs an interactive simulation of some organisation'
    mainClass = 'jason.infra.centralised.RunCentralisedMAS'
    args 'org-simulator.mas2j'
    classpath sourceSets.main.runtimeClasspath
    workingDir project.projectDir.absolutePath + '/examples/sim'
}

task sim (type: JavaExec, dependsOn: 'jar') {
    description 'runs a program that simulates some dynamics of an organisation'
    mainClass = 'ora4mas.nopl.simulator.ConsoleSimulator'
    classpath sourceSets.main.runtimeClasspath
}

task os2dot (type: JavaExec, dependsOn: 'jar') {
    mainClass = 'moise.tools.os2dotGUI'
    classpath sourceSets.main.runtimeClasspath
}

eclipse {
    classpath {
        downloadJavadoc = true
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    baseName 'moise-exceptions'
    classifier = 'javadoc'
    from '${docsDir}/../../doc/api'
}

task sourceJar (type : Jar) {
    baseName 'moise-exceptions'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task fixTab {
    ant.fixcrlf(eol: 'lf',  includes: '**/*.txt,**/*.bat, **/*.adoc', srcdir: '.')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.xsl,**/*.xsd', srcdir: 'src')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.xsl,**/*.xsd', srcdir: 'examples')
}

task renderAsciiDocs(type: Exec) {
    commandLine 'find', '.', '-name', '*.adoc', '-exec', 'asciidoctor', '{}', ';'
}

javadoc {
    options.showAll()
    options.encoding('UTF-8')
    options.setUse(true)
    options.author(true)
    options.version(true)
    options.windowTitle('Moise API')
    options.docTitle('Moise API')
    options.footer('<a href=http://moise.sf.net>Moise Site</a>')
    destinationDir = file("${buildDir}/../doc/api")
    doFirst {
        javadoc.title = 'Moise API'
        javadoc.options.docTitle = javadoc.title
    }
}

// the following lines is used to avoid errors while generating javadoc
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
}


task publishToMavenSF(dependsOn: ['build', 'publishToMavenLocal']) {
	doLast {

    	def wdir = System.getProperty("user.home")+'\\.m2\\repository\\it\\unito\\di\\moise-exceptions\\'
	    def tdir = project.findProperty("sf.user")+'@web.sourceforge.net:/home/project-web/moise-exceptions/htdocs/maven2/it/unito/di/moise-exceptions/'

        file(wdir+'maven-metadata-local.xml').renameTo(file(wdir+'maven-metadata.xml'))

		ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
            classpath: configurations.sshAntTask.asPath)

	    // Invoke the scp Ant task. (Use gradle -i update to see the output of the Ant task.)
	    ant.scp(todir: tdir,
	            password: project.findProperty("sf.pass"),
	            trust: "yes",
	            verbose: 'true') {
                    fileset(dir: wdir) {
                        include(name: '**/**')
                    }
	    }

        file(wdir+'maven-metadata.xml').renameTo(file(wdir+'maven-metadata-local.xml'))

	}
}
