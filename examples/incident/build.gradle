/*
   example: incident

   Gradle build file for JaCaMo Application
   September 27, 2018 - 15:33:36
*/

plugins {
    id 'java'
    id 'eclipse'
}

defaultTasks 'run'

version '1.0'
group   'it.unito.di'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    //mavenLocal()

    maven {
        url "http://jacamo.sourceforge.net/maven2"
        allowInsecureProtocol = true
    }
    maven { url "https://moise-exceptions.sourceforge.io/maven2" }
    //maven { url "http://www.di.unito.it/~tedeschi/maven2" }

    flatDir {
       dirs 'lib'
    }
}

dependencies {
    implementation group: 'org.jacamo',   name: 'jacamo',          version: '0.8'
    implementation group: 'org.jacamo',   name: 'npl' ,            version: '0.5-SNAPSHOT', changing: true, transitive: false
    implementation group: 'it.unito.di', name: 'moise-exceptions', version: '0.2',          changing: true
}

configurations {
    compile.exclude group: 'org.jacamo', module: 'moise'
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

sourceSets {
    main {
        java {
            srcDir 'src/env'
            srcDir 'src/agt'
            srcDir 'src/org'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

task run (type: JavaExec, dependsOn: 'classes') {
    description 'runs the application'
    group ' JaCaMo'
    mainClass = 'jacamo.infra.JaCaMoLauncher'
    args 'incident.jcm'
    classpath sourceSets.main.runtimeClasspath
}

clean {
    delete 'bin'
    delete 'build'
}
